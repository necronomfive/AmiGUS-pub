.KEY ""
.BRA {
.KET }

FAILAT 10

; *e[30m - blue
; *e[31m - white
; *e[32m - black
; *e[33m - orange

RESIDENT C:ECHO
RESIDENT C:ASK
RESIDENT C:ASSIGN
RESIDENT C:IF
RESIDENT C:ENDIF
RESIDENT C:EDIT

ASSIGN AmiGUS-13-SRC: ""

ECHO "Welcome to the AmiGUS OS 1.3 install script."
ECHO ""
ECHO "If you want to abort, please press CTRL-C at any point."
ECHO "If anything is unclear, please refer to the ReadMeFirst.txt"
ECHO "or check the manual."
ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                      Base tools installation                      |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""
ASK "Do you want to install the AmiGUS base tools? (y/N)"

IF WARN
  ECHO "The default installation path for the AmiGUS base tools is:"
  ECHO "*e[33mSYS:Expansion/AmiGUS*e[31m"
  ECHO "However, this may cause issues with ... very well written software."
  ECHO ""
  ASK "Do you want to adapt the path before installing (y/N)?"
 
  IF WARN
    ECHO "Enter your desired installation path and push Return:"
    SETENV >NIL: TOOLSPATH ?
  ELSE
    SETENV >NIL: TOOLSPATH "SYS:Expansion/AmiGUS"
  ENDIF

  ECHO   >  RAM:AmiGUS_installer1 "IF NOT EXISTS TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "  MAKEDIR TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "ENDIF"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:Tools/#? TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:Tools.info TOOLSPATH.info"

  ; Prepare EDIT script to replace TOOLSPATH with correct path in installer script
  ECHO   >  RAM:AmiGUS_editscript1 "GE ?TOOLSPATH?" NOLINE
  GETENV >> RAM:AmiGUS_editscript1 TOOLSPATH
  ECHO   >> RAM:AmiGUS_editscript1 "?" NOLINE

  ; Prepare 2nd EDIT script to put 1st EDIT script into single line... *sigh*
  ECHO   >  RAM:AmiGUS_editscript2 "CL //"
  
  ; Run 2nd EDIT script on 1st EDIT script
  EDIT >NIL: FROM RAM:AmiGUS_editscript1 TO RAM:AmiGUS_editscript3 WITH RAM:AmiGUS_editscript2

  ; Run 1st EDIT script on installer script
  EDIT >NIL: FROM RAM:AmiGUS_installer1 TO RAM:AmiGUS_installer2 WITH RAM:AmiGUS_editscript3

  ; Run installer script
  EXECUTE RAM:AmiGUS_installer2

  ; Clean that mess up again...
  DELETE >NIL: RAM:AmiGUS_editscript#? RAM:AmiGUS_installer#?
ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                     Debug tools installation                      |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""
ASK "Do you want to install the AmiGUS debug tools? (y/N)"

IF WARN
  ECHO "The default installation path for the AmiGUS debug tools is:"
  ECHO "*e[33mSYS:Expansion/AmiGUS/debug*e[31m"
  ECHO "However, this may cause issues with ... very well written software."
  ECHO ""
  ASK "Do you want to adapt the path before installing? (y/N)"
 
  IF WARN
    ECHO "Enter your desired installation path and push Return:"
    SETENV >NIL: TOOLSPATH ?
  ELSE
    SETENV >NIL: TOOLSPATH "SYS:Expansion/AmiGUS/debug"
  ENDIF

  ECHO   >  RAM:AmiGUS_installer1 "IF NOT EXISTS TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "  MAKEDIR TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "ENDIF"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:MHI/MHIPlay#? TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:MHI/MHILibCheck#? TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:MHI/GetMhiMemLog#? TOOLSPATH"
  ECHO   >> RAM:AmiGUS_installer1 "COPY AmiGUS-13-SRC:Tools.info TOOLSPATH.info"

  ; Prepare EDIT script to replace TOOLSPATH with correct path in installer script
  ECHO   >  RAM:AmiGUS_editscript1 "GE ?TOOLSPATH?" NOLINE
  GETENV >> RAM:AmiGUS_editscript1 TOOLSPATH
  ECHO   >> RAM:AmiGUS_editscript1 "?" NOLINE

  ; Prepare 2nd EDIT script to put 1st EDIT script into single line... *sigh*
  ECHO   >  RAM:AmiGUS_editscript2 "CL //"
  
  ; Run 2nd EDIT script on 1st EDIT script
  EDIT >NIL: FROM RAM:AmiGUS_editscript1 TO RAM:AmiGUS_editscript3 WITH RAM:AmiGUS_editscript2

  ; Run 1st EDIT script on installer script
  EDIT >NIL: FROM RAM:AmiGUS_installer1 TO RAM:AmiGUS_installer2 WITH RAM:AmiGUS_editscript3

  ; Run installer script
  EXECUTE RAM:AmiGUS_installer2

  ; Clean that mess up again...
  DELETE >NIL: RAM:AmiGUS_editscript#? RAM:AmiGUS_installer#?
ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                      MHI driver installation                      |"
ECHO "+-------------------------------------------------------------------+"
ECHO ""
ECHO "OS 1.3 is a bit ... picky ... about loading libraries."
ECHO "To make MHI work reliably, the MHI driver needs "
ECHO "to be installed to the default library location - LIBS:."
ECHO ""
ASK "Do you want to install mhiamigus.library to LIBS:? (y/N)"

IF WARN
  ECHO "Which MHI driver do you want to install?"
  ECHO ""
  ECHO "1) 68000, RELEASE version, no logging, RECOMMENDED!!!"
  ECHO "2) 68000, DEBUG version, serial logging"
  ECHO "3) 68000, DEBUG version, file logging"
  ECHO "4) 68000, DEBUG version, memory logging"
  ECHO "5) 68020, RELEASE version, no logging, use this if on 68020+"
  ECHO "6) 68020, DEBUG version, serial logging"
  ECHO "7) 68020, DEBUG version, file logging"
  ECHO "8) 68020, DEBUG version, memory logging"
  ECHO ""
  ECHO "*e[33mDefault: 1*e[31m - please enter 1-8 and push Return:"
  SETENV >NIL: mhi_variant ?

  IF $mhi_variant EQ 2
    COPY MHI/mhiamigus.library-000-SER_LOG LIBS:mhiamigus.library CLONE
    ECHO "Option 2 copied..."
  ELSE
    IF $mhi_variant EQ 3
      COPY MHI/mhiamigus.library-000-FILE_LOG LIBS:mhiamigus.library CLONE
      ECHO "Option 3 copied..."
    ELSE
      IF $mhi_variant EQ 4
        COPY MHI/mhiamigus.library-000-MEM_LOG LIBS:mhiamigus.library CLONE
        ECHO "Option 4 copied..."
      ELSE
        IF $mhi_variant EQ 5
          COPY MHI/mhiamigus.library-020-NO_LOG LIBS:mhiamigus.library CLONE
          ECHO "Option 5 copied..."
        ELSE
          IF $mhi_variant EQ 6
            COPY MHI/mhiamigus.library-020-SER_LOG LIBS:mhiamigus.library CLONE
            ECHO "Option 6 copied..."
          ELSE
            IF $mhi_variant EQ 7
              COPY MHI/mhiamigus.library-020-FILE_LOG LIBS:mhiamigus.library CLONE
              ECHO "Option 7 copied..."
            ELSE
              IF $mhi_variant EQ 8
                COPY MHI/mhiamigus.library-020-MEM_LOG LIBS:mhiamigus.library CLONE
                ECHO "Option 8 copied..."
              ELSE
                COPY MHI/mhiamigus.library-000-NO_LOG LIBS:mhiamigus.library CLONE
                ECHO "Option 1 copied..."
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
ENDIF

ECHO ""
ECHO "Do you want to copy all variants of mhiamigus.library to LIBS: for"
ASK "potential later use after renaming? (y/N)"

IF WARN
  copy MHI/mhiamigus.library#? LIBS:
ENDIF

ECHO ""
ECHO "+-------------------------------------------------------------------+"
ECHO "|                    All done - have a nice day!                    |"
ECHO "+-------------------------------------------------------------------+"

ENDCLI
