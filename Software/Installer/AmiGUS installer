(complete 0)
(delopts "oknodelete" "force" "askuser")

;==============================================================================
; English strings

(set default_lang 4)

(set #which_drivers_target
(cat "Drivers will need to be installed to the corresponding system folders, "
     "LIBS:, DEVS:, etc..\n"
     "In which system disk shall the AmiGUS drivers be installed?\n\n"
))

(set #which_drivers_target_help
(cat "\nThis section lets you choose in which drawer or on which disk the "
     "AmiGUS driver files should be installed. These should normally go "
     "in the same location as the current Workbench, as that is where AHI "
     "and MHI would expect them.\n\n"
     @askdir-help
))

(set #which_tools_target
(cat "The AmiGUS tools can be installed wherever you like them to be - "
     "so please select the your prefered location.\n"
     "(A folder called \"AmiGUS\" will be created there.)\n\n"
))

(set #which_tools_target_help
(cat "\nThis section lets you choose in which drawer or on which disk the "
     "AmiGUS base tools should be installed. It can be anywhere you like.\n\n"
     @askdir-help
))

;==============================================================================
; German strings

(if (= @language "deutsch")
(
(set default_lang 2)

(set #which_drivers_target
(cat "Die Treiber werden in die passenden Systemverzeichnisse installiert, "
     "LIBS:, DEVS:, etc..\n"
     "Auf welches Systemlaufwerk sollen die AmiGUS Treiber installiert "
     "werden?\n\n"
))

(set #which_drivers_target_help
(cat "\nIn diesem Teil können Sie wählen, auf welche Laufwerk oder "
     "in welches Verzeichnis die AmiGUS Treiber installiert werden sollen. "
     "Sie sollten normalerweise an dieselbe Stelle installiert werden "
     "wie die aktuelle Workbench, weil das ist, wo AHI und MHI sie "
     "erwarten würden.\n\n"
     @askdir-help
))

(set #which_tools_target
(cat "Die AmiGUS Werkzeuge können an einen beliebigen Ort installiert werden "
     "- suchen Sie bitte ihren bevorzugten Ort aus.\n"
     "(Ein Verzeichnis \"AmiGUS\" wird dort angelegt.)\n\n"
))

(set #which_tools_target_help
(cat "\nIn diesem Teil können Sie wählen, auf welche Laufwerk oder "
     "Verzeichnis die AmiGUS Basiswerkzeuge installiert werden sollen. "
     "Der Ort ist dabei absolut beliebig. \n\n"
     @askdir-help
))

))

;=============================================================================
(set cpu_found (database "cpu"))
(if (OR (= cpu_found "68000") (= cpu_found "68010"))
  (set cpu_default 0)
  (if (OR (= cpu_found "68020") (= cpu_found "68030"))
      (set cpu_default 1)
      (set cpu_default 2)
  )
)

(procedure install_driver (
  (set install_driver_cpu cpu_default)
  (set install_driver_debug 0)
  (set install_driver_all 0)

  (if
    (>= @user-level 1)
    (
      (set install_driver_cpu 
        (askchoice
          (prompt "Please select which CPU version of "driver_name" shall be installed.")
          (help "my help")
          (choices "68000" "68020" "68040")
          (default install_driver_cpu)
        )
      )

      (set install_driver_debug
        (askchoice
          (prompt "Do you want to use a driver with debug output enabled? "
                  "Please only do so if you know what you are doing or were asked to do so.\n"
                  "(WARNING: SLOWER!)\n\n"
          )
          (help "my help")
          (choices "No - release is fine(RECOMMENDED)"
                   "Yes - serial debug output"
                   "Yes - file logging"
                   "Yes - memory logging")
          (default install_driver_debug)
        )
      )
    )
  )

  (if
    (>= @user-level 2)
    (set install_driver_all
      (askbool
        (prompt "Do you want to copy all "driver_name" versions next to the active one for later potential use?")
        (help "more help")
        (choices "Yes" "No")
        (default install_driver_all)
      )
    )
  )

  (set install_driver_cpu (select install_driver_cpu "-000" "-020" "-040"))
  (set install_driver_debug (select install_driver_debug "-NO_LOG" "-SER_LOG" "-FILE_LOG" "-MEM_LOG"))
))


; say hi!
(welcome)
(set old_level @user-level)

(onerror (TERMINATE))

(set what (askoptions (prompt "Please select what should be installed.")
                               (help "my help")
                               (choices "AHI driver"
                                        "MHI driver"
                                        "Mixer"
                                        "FlashGUI"
                                        "PlayWAV"
                                        "PlayMP3"
                                        "PlayMIDI"
                                        "Record"
                                        "Debug Tools")
                               (default %110111111)
                               ))

(if
  (BITAND what %000000011)
  (
    (set drivers_target
      (askdir
        (prompt #which_drivers_target)
        (help #which_drivers_target_help)
        (default "SYS:")
      )
    )
  )
)

(if
  (BITAND what %000000001)
  (
    (set driver_name "AHI driver")
    (install_driver)

    (set source_folder "AHI4/AHI")
    (set file "AmiGUS.audio")
    (set target_folder (tackon drivers_target "Devs/AHI"))
    (set source_file (tackon source_folder file))

    (makedir target_folder)
    (copyfiles
      (source (cat source_file install_driver_cpu install_driver_debug))
      (newname file)
      (dest target_folder)
    )

    (if install_driver_all
      (copyfiles
        (source source_folder)
        (pattern "#?")
        (dest target_folder)
      )  
    )

    (set source_folder "AHI4/AudioModes")
    (set file "AMIGUS")
    (set target_folder (tackon drivers_target "Devs/AudioModes"))
    (set source_file (tackon source_folder file))

    (makedir target_folder)
    (copyfiles
      (source source_file)
      (dest target_folder)
    )
  )
)

(complete 15)

(if
  (BITAND what %000000010)
  (
    (set driver_name "MHI driver")
    (install_driver)

    (set source_folder "MHI")
    (set file "mhiamigus.library")
    (set target_folder (tackon drivers_target "Libs/MHI"))
    (set source_file (tackon source_folder file))

    (makedir target_folder)
    (copyfiles
      (source (cat source_file install_driver_cpu install_driver_debug))
      (newname file)
      (dest target_folder)
    )
    
    (if install_driver_all
      (copyfiles
        (source source_folder)
        (pattern "#?")
        (dest target_folder)
      )
    )
  )
)

(complete 30)

(if
  (BITAND what %111111100)
  (
    (set tools_target
      (askdir (prompt #which_tools_target)
        (help #which_tools_target_help)
        (default "SYS:Expansion")
      )
    )
    (set tools_target (tackon tools_target "AmiGUS"))
    (makedir tools_target (infos))
    (set source_folder "Tools")
  )
)

(if
  (BITAND what %000000100)
  (copyfiles
    (source (tackon source_folder "Mixer"))
    (dest tools_target)
    (infos)
  )
)

(complete 40)

(if
  (BITAND what %000001000)
  (copyfiles
    (source (tackon source_folder "FlashGUI"))
    (dest tools_target)
    (infos)
  )
)

(complete 50)

(if
  (BITAND what %000010000)
  (copyfiles
    (source (tackon source_folder "PlayWAV"))
    (dest tools_target)
    (infos)
  )
)

(complete 60)

(if
  (BITAND what %000100000)
  (copyfiles
    (source (tackon source_folder "PlayMP3"))
    (dest tools_target)
    (infos)
  )
)

(complete 70)

(if
  (BITAND what %001000000)
  (copyfiles
    (source (tackon source_folder "PlayMIDI"))
    (dest tools_target)
    (infos)
  )
)

(complete 80)

(if
  (BITAND what %010000000)
  (copyfiles
    (source (tackon source_folder "Record"))
    (dest tools_target)
    (infos)
  )
)

(complete 90)

(if
  (BITAND what %100000000)
  (copyfiles
    (source (tackon source_folder "DebugTools"))
    (dest tools_target)
    (infos)
  )
)

(complete 100)

